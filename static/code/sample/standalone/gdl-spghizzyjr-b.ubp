module main
author unknown
version 1 0 
description ''

script 201 121 {
whenStarted
forever {
  if (dueSTEM_LDR_button) {
    due_ghizzyjr_set_left_eye 128 0 0
    due_ghizzyjr_set_right_eye 0 0 128
    waitMillis 100
    due_ghizzyjr_set_left_eye 0 0 128
    due_ghizzyjr_set_right_eye 128 0 0
    waitMillis 100
  } else {
    due_ghizzyjr_set_left_eye 128 0 128
    due_ghizzyjr_set_right_eye 0 128 128
    waitMillis 500
    due_ghizzyjr_set_left_eye 0 128 128
    due_ghizzyjr_set_right_eye 128 0 128
    waitMillis 500
  }
}
}


module 'DUELink Edu'
author MicroBlocks
version 1 12 
choices dueSTEM_pianoPad left right C 'C#' D 'D#' E F 'F#' G 'G#' A 'A#' B 'High C' 
choices dueSTEM_clipItPad A B left right up down 
choices dueSTEM_buttonName left right up down 
choices dueSTEM_axis x y z 
choices dueSTEM_timeComponent 'time (H:M:S)' 'date (Y-M-D)' second minute hour day month year 'day of week' 
description 'Library for DUELink Educational boards: CincoBit, PixoBit, ClipIt, and DueSTEM.

The "due PID" works on all DUELink boards.
Blocks labeled "dueSTEM" work only on the DueSTEM board.

Touch sensing works only on pins with capacitive touch circuits including pins 0, 1, 2 of the CincoBit and PixoBit and all but the SCL/SDA pins of the ClipIt.
'
variables dueSTEM_accelAddr dueSTEM_touchAdjustments 

  spec 'r' 'dueSTEM_light' 'due light level (0-1000)'
  spec 'r' 'dueSTEM_LDR_button' 'due LDR button'
  spec 'r' 'dueSTEM_buzzerPin' 'due buzzer pin'
  spec 'r' 'dueSTEM_PID' 'due PID'
  space
  spec 'r' 'dueSTEM_touched' 'due pin _ touched' 'num' 1
  spec ' ' 'dueSTEM_waitForRelease' 'due wait for pin _ release' 'auto' 1
  spec 'r' 'dueSTEM_clipitPadPin' 'Clipit _ pin' 'menu.dueSTEM_clipItPad' 'A'
  spec 'r' 'dueSTEM_pianoPadPin' 'Piano _ pin' 'menu.dueSTEM_pianoPad' 'C'
  space
  spec ' ' 'dueSTEM_setLED' 'DueSTEM bulb red _ green _ blue _ (0-1000)' 'auto auto auto' 100 0 200
  spec 'r' 'dueSTEM_button' 'DueSTEM _ button' 'menu.dueSTEM_buttonName' 'left'
  spec 'r' 'dueSTEM_tilt' 'DueSTEM tilt _' 'menu.dueSTEM_axis' 'x'
  spec 'r' 'dueSTEM_temperature' 'DueSTEM temperature (x10)'
  space
  spec 'r' 'dueSTEM_getTimeOrDate' 'due clock _' 'menu.dueSTEM_timeComponent' 'time (H:M:S)'
  spec ' ' 'dueSTEM_setTime' 'due set hours _ minutes _ seconds _' 'num num num' 9 30 0
  spec ' ' 'dueSTEM_setDate' 'due set year _ month _ day _ weekday _' 'num num num num' 2025 1 1 1
  advanced
  spec ' ' 'dueSTEM_shutdown' 'low power shutdown'
  space
  spec ' ' '_dueSTEM_accelInit' '_dueSTEM_accelInit'
  spec 'r' '_dueSTEM_accelRead16BitReg' '_dueSTEM_accelRead16BitReg _' 'auto' 13
  spec 'r' '_dueSTEM_adjustmentForPin' '_dueSTEM_adjustmentForPin _' 'num' 1
  spec 'r' '_dueSTEM_dischargeTime' '_dueSTEM_dischargeTime _' 'num' 1

to '_dueSTEM_accelInit' {
  comment 'MC3216
https://www.mouser.com/datasheet/2/821/MEMSIC_MC3216_Datasheet-1879973.pdf'
  if (dueSTEM_accelAddr != 0) {
    return
  }
  dueSTEM_accelAddr = (hexToInt '4C')
  comment 'Turn off accelerometer before changing settings'
  i2cSet dueSTEM_accelAddr 7 0
  comment 'Sampling rate: 256 Hz'
  i2cSet dueSTEM_accelAddr 8 10
  comment '+/-2G range, 10-bit resolution'
  i2cSet dueSTEM_accelAddr (hexToInt '20') 3
  comment 'Start accelerometer'
  i2cSet dueSTEM_accelAddr 7 1
}

to '_dueSTEM_accelRead16BitReg' reg {
  '_dueSTEM_accelInit'
  local 'buf' (newList 2)
  '[sensors:i2cWrite]' (hexToInt '4C') ('[data:makeList]' reg) false
  '[sensors:i2cRead]' (hexToInt '4C') buf
  local '16bit' (((at 2 buf) << 8) | (at 1 buf))
  '16bit' = (((v '16bit') << 15) >> 15)
  return ((100 * (v '16bit')) / 255)
}

to '_dueSTEM_adjustmentForPin' pin {
  if (dueSTEM_touchAdjustments == 0) {dueSTEM_touchAdjustments = (newList (digitalPins) -1)}
  local 'result' (at (pin + 1) dueSTEM_touchAdjustments)
  if (result >= 0) {
    return result
  }
  local 'total usecs' 0
  repeat 80 {
    'total usecs' += ('_dueSTEM_dischargeTime' pin)
  }
  result = (maximum 0 (minimum (((v 'total usecs') / 80) - 99) 30))
  atPut (pin + 1) dueSTEM_touchAdjustments result
  return result
}

to '_dueSTEM_dischargeTime' pin {
  comment 'Charge up pin'
  digitalWriteOp pin true
  local 'startT' (microsOp)
  waitUntil (not (digitalReadOp pin))
  local 'usecs' (microsSince startT)
  digitalWriteOp pin true
  return usecs
}

to dueSTEM_LDR_button {
  if (or ((boardType) == 'CincoBit') ((boardType) == 'PixoBit')) {return (buttonA)}
  return (digitalReadOp 20 'down')
}

to dueSTEM_PID {
  local 'result' ('[data:makeList]')
  for i 6 {
    local 'hexDigit' ((('[misc:dueLinkPID]') >> (24 - (4 * i))) & 15)
    '[data:addLast]' (at (hexDigit + 1) '0123456789ABCDEF') result
  }
  return ('[data:joinStrings]' result)
}

to dueSTEM_button which {
  if ((boardType) != 'DueSTEM') {return (booleanConstant false)}
  if ('left' == which) {
    return (digitalReadOp 20 'down')
  } ('right' == which) {
    return (digitalReadOp 1 'down')
  } ('up' == which) {
    return (digitalReadOp 18 'down')
  } ('down' == which) {
    return (digitalReadOp 10 'down')
  }
  return (booleanConstant false)
}

to dueSTEM_buzzerPin {
  if (or ((boardType) == 'CincoBit') ((boardType) == 'PixoBit')) {
    return 21
  } ((boardType) == 'DueSTEM') {
    return 3
  } ((boardType) == 'Clipit') {
    return 7
  } ((dueSTEM_PID) == '0C0001') {
    comment 'Ghizzy'
    return 3
  } ((dueSTEM_PID) == '0C0003') {
    comment 'Holiday Tree'
    return 1
  } ((dueSTEM_PID) == '0C0005') {
    comment 'Piano'
    return 3
  } ((dueSTEM_PID) == '070001') {
    comment 'Buzzer'
    return 7
  }
  return -1
}

to dueSTEM_clipitPadPin padName {
  if ('A' == padName) {
    return 10
  } ('B' == padName) {
    return 24
  } ('left' == padName) {
    return 14
  } ('right' == padName) {
    return 12
  } ('up' == padName) {
    return 13
  } ('down' == padName) {
    return 18
  } ('P1' == padName) {
    return 1
  } ('P2' == padName) {
    return 2
  } ('P3' == padName) {
    return 3
  }
  return 10
}

to dueSTEM_getTimeOrDate what {
  local 'timeArray' ('[misc:dueGetTime]')
  if ('time (H:M:S)' == what) {
    return ('[data:join]' (at 5 timeArray) ':' (at 6 timeArray) ':' (at 7 timeArray))
  } ('date (Y-M-D)' == what) {
    return ('[data:join]' (at 1 timeArray) ':' (at 2 timeArray) ':' (at 3 timeArray))
  } ('year' == what) {
    return (at 1 timeArray)
  } ('month' == what) {
    return (at 2 timeArray)
  } ('day' == what) {
    return (at 3 timeArray)
  } ('day of week' == what) {
    return (at 4 timeArray)
  } ('hour' == what) {
    return (at 5 timeArray)
  } ('minute' == what) {
    return (at 6 timeArray)
  } ('second' == what) {
    return (at 7 timeArray)
  }
}

to dueSTEM_light {
  return (minimum ('[display:lightLevel]') 1000)
}

to dueSTEM_pianoPadPin padName {
  if ('left' == padName) {
    return 11
  } ('right' == padName) {
    return 7
  } ('C' == padName) {
    return 23
  } ('C#' == padName) {
    return 19
  } ('D' == padName) {
    return 12
  } ('D#' == padName) {
    return 13
  } ('E' == padName) {
    return 14
  } ('F' == padName) {
    return 15
  } ('F#' == padName) {
    return 16
  } ('G' == padName) {
    return 18
  } ('G#' == padName) {
    return 24
  } ('A' == padName) {
    return 10
  } ('A#' == padName) {
    return 9
  } ('B' == padName) {
    return 8
  } ('High C' == padName) {
    return 17
  }
  return 23
}

to dueSTEM_setDate year month day dayOfWeek {
  '[misc:dueSetDate]' year month day dayOfWeek
}

to dueSTEM_setLED r g b {
  if ((boardType) != 'DueSTEM') {return 0}
  analogWriteOp 11 r
  analogWriteOp 4 g
  analogWriteOp 2 b
}

to dueSTEM_setTime hour minute second {
  '[misc:dueSetTime]' hour minute second
}

to dueSTEM_shutdown {
  comment 'Enter low-power sleep mode. Can be awoken by a low-to-high
transition on P1 (PA0) or P3 (PA4). On DUELink boards with edge
connectors those are the alligator clip holes marked P0 and P2.'
  comment 'Note: Low power mode cannot be entered for the first five seconds
after reset or power up The ensure that the IDE can connect to the
board even if this function is called by a "when started" block.'
  waitUntil ((secsOp) > 5)
  '[misc:dueSleep]'
}

to dueSTEM_temperature {
  if ((boardType) != 'DueSTEM') {return 0}
  local 'mVx10' ((330000 * (analogReadOp 9)) / 1023)
  return ((mVx10 - 40000) / 195)
}

to dueSTEM_tilt axis {
  if ((boardType) != 'DueSTEM') {return 0}
  if ('x' == axis) {
    return ('_dueSTEM_accelRead16BitReg' 13)
  } ('y' == axis) {
    return (0 - ('_dueSTEM_accelRead16BitReg' 15))
  } ('z' == axis) {
    return ('_dueSTEM_accelRead16BitReg' 17)
  }
  return 0
}

to dueSTEM_touched pin {
  local 'touch adjustment' ('_dueSTEM_adjustmentForPin' pin)
  comment 'Charge up pin'
  digitalWriteOp pin true
  comment 'Switch pin to input mode. Capacitor starts
discharging through 1M resistor to ground.'
  local 'ignore' (digitalReadOp pin)
  comment 'Tricky code: MicroBlocks does not switch tasks for waits of 30 usecs
or less so the following four blocks are essentially a busy wait. Because
of overhead, this takes ~140 usecs on DUELink boards. If the pin is still
high after that time, it is because it is being touched, thus increasing
the discharge time of the total effective capacitance of the pin.'
  waitMicros 30
  waitMicros 30
  waitMicros 2
  waitMicros (v 'touch adjustment')
  return (digitalReadOp pin)
}

to dueSTEM_waitForRelease pin {
  comment 'There is a lot of electrical noise while a pin is being touched.
That noise causes "due pin touched" to sometimes return false
even though the pin is still being touched. Experimentation
determined that it requires 60 to 70 "false" reports in a row to
be certain that the pin is not being touched. Those tests take
about 16 milliseconds total on an otherwise idle DUELink board.'
  local 'releaseCount' 0
  forever {
    if (dueSTEM_touched pin) {
      releaseCount = 0
    } else {
      releaseCount += 1
      if (releaseCount >= 80) {
        return 0
      }
    }
  }
}


module 'DUELink Ghizzy Jr'
author GHIElectronics
version 0 5 
description 'DUELink Ghizzy Jr Library

'
variables _due_ghizzyjr_enabled _due_ghizzyjr_oldver 

  spec ' ' 'due_ghizzyjr_set_left_eye' 'set left eye red _ green _ blue _' 'num num num' 128 128 128
  spec ' ' 'due_ghizzyjr_set_right_eye' 'set right eye red _ green _ blue _' 'num num num' 128 128 128
  spec 'r' 'due_ghizzyjr_button' 'button pressed'
  space
  spec ' ' '_due_ghizzyjr_initialize' '_initialize'

to '_due_ghizzyjr_initialize' {
  if (_due_ghizzyjr_enabled != 1) {
    _due_ghizzyjr_enabled = 1
    local 'value' 0
    _due_ghizzyjr_oldver = (digitalReadOp 17 'up')
  }
}

to due_ghizzyjr_button {
  return ('[data:convertType]' (digitalReadOp 20 'down') 'boolean')
}

to due_ghizzyjr_set_left_eye red green blue {
  '_due_ghizzyjr_initialize'
  analogWriteOp 8 ('[misc:rescale]' red 0 255 0 1023)
  analogWriteOp 7 ('[misc:rescale]' blue 0 255 0 1023)
  if _due_ghizzyjr_oldver {
    analogWriteOp 9 ('[misc:rescale]' green 0 255 0 1023)
  } else {
    analogWriteOp 1 ('[misc:rescale]' green 0 255 0 1023)
  }
}

to due_ghizzyjr_set_right_eye red green blue {
  analogWriteOp 5 ('[misc:rescale]' red 0 255 0 1023)
  analogWriteOp 6 ('[misc:rescale]' green 0 255 0 1023)
  analogWriteOp 4 ('[misc:rescale]' blue 0 255 0 1023)
}

