---
sidebar_position: 18
title: TFT CP23
description: TFT CP23 - High-quality DUELink module
hide_table_of_contents: true
pagination_next: null
pagination_prev: null
---

import Tabs from '@theme/Tabs';
import TabItem from '@theme/TabItem';
import ImageSection from '@site/src/components/ImageSection';
import OrderSection from '@site/src/components/OrderSection';

# TFT CP23

<ImageSection 
  product="dtftcp23-b"
  tagline="2.3"
/>

---

<Tabs className="unique-tabs" groupid="catalog" queryString="show" defaultValue="overview"
  values={[
    {
        label: "Overview",
        value: "overview"
    },
    {
        label: "Drivers",
        value: "drivers"
    },
    {
        label: "Samples",
        value: "samples"
    }
]}>

<TabItem value="overview">

2.3" 320x240 color TFT display with capacitive touch screen, supporting Graphics in buffered and in direct modes.

<table><td width='50%'>
**Key features**

â€¢ Full Color 320x240 Pixels<br/>
â€¢ Capacitive Multi-Touch<br/>
â€¢ 67x47mm<br/>

</td><td width='50%'>
**Resources**
 
ðŸ“„<a href="/sch/gdl-dtftcp23-b.pdf">Schematics</a><br/>
ðŸ”©<a href="/3d/gdl-dtftcp23-b.step">3D STEP file</a><br/>
</td></table>

</TabItem>

<TabItem value="drivers">

See [Drivers](/docs/engine/drivers) page for further details.

| Function | Description |
|----------|-------------|
| `Init(m,o)` | Initialize display `m` mode (buffered = 0 , direct = 1). `o` orientation (landscape = 0, portrait = 1). Init(0, 0) automatically called on power-up. |
| `GetW()` | Get display width: 106 in buffered, 320 in direct. |
| `GetH()` | Get display height: 80 in buffered, 240 in direct. |
| `IsColor()` | Returns 1. |
| `TouchX()` | Returns touch x. |
| `TouchY()` | Returns touch y. |
| `SetDACtrl(s,r,c,b)` | Data access control `s` swapRowCol, `r` invertRow, `c` invertCol, `b` swapBGRSet data access control. |
| `SetWindow(x,y,w,h)` | Sets draw window `x` display's x value, `y` display's y value., `w` display's width value, `h` display's height value. |
| `DVer()` | This driver version. |
<details>
<summary><strong>The Code!</strong></summary>

**TFT CP23 Driver**

```batch
##### TFT CP23 #####

fn DVer()
    return 1.0
fend

Dim b1[2]
_s = 5
_r = 6
_m = 1
_x = 0
_y = 0
_p = 0

fn Init(m, p)
    DWrite(4,1)
    DWrite(7,1)
    SpiCfg(0, 24000)
    _m=m
    _p=p

    if m = 0 # buffered
	    GfxCfg(2, {_s,_r},GetW(),GetH(), 3)
    else # direct
	    GfxCfg(2,{_s,_r},GetW(),GetH(), 0)
    end
    
	LcdCmd(0xc8, [0xFF])
	LcdCmd(0x93, [0xFF])
	LcdCmd(0x36, [0xc8])
	LcdCmd(0x3a, [0x55])
	LcdCmd(0xc0, [0x10,0x10])
	LcdCmd(0xc1, [0x36])
	LcdCmd(0xc5, [0xc3])
	LcdCmd(0xE0, [0x00,0x05,0x08,0x02,0x1a,0x0c,0x42,0x7a,0x54,0x08,0x0d,0x0c,0x23,0x25,0x0f])
	LcdCmd(0xE1, [0x00,0x29,0x2f,0x03,0x0f,0x05,0x42,0x55,0x53,0x06,0x0f,0x0c,0x38,0x3a,0x0f])
	LcdCmd(0x11,[])
	Wait(120)
	LcdCmd(0x36, [0xc8])
	LcdCmd(0x2a, [0x00,0x00,0x01,0x3d])
	LcdCmd(0xE1, [0x00,0x00,0x00,0xef])
    Wait(120)
    
    if (_p = 0)
        SetDACtrl(0,1,1,1)
        SetWindow(0,0,320-1,240-1)
    else
        SetDACtrl(1,1,0,1)
        SetWindow(0,0,240-1,320-1)
    end
    
	LcdCmd(0x29,[])

	i2cCfg(400)
	IStart("TouchIrq", 2, 0, 1)

    Clear(0)
    TextS("DUELink",1,60,90,5,7)
    Show()
fend

fn LcdCmd(c, b1)
    ##SendLcdCmd(c)
    DWrite(_s, 0)#select
    DWrite(_r, 0)#LcdCmd
    SpiWr(c)
    DWrite(_r, 1)#data
    
    for i in Range(0,Len(b1))
        SpiWr(b1[i])
    next
    
    DWrite(_s, 1)#deselect
fend

fn IsColor()
    return 1
fend

fn TouchIrq() 
    Dim b1[1] = [0]
    Dim b2[32]
    
    I2cWr(0x38, b1, 0)
    
    I2cWr(0x38, 0, b2)
    
    for i = 0 to 1
        d = i * 6 + 3
        f = (b2[0 + d] & 0xC0) >> 6
        x = ((b2[0 + d] & 0x0F) << 8) | b2[1 + d]
        y = ((b2[2 + d] & 0x0F) << 8) | b2[3 + d]
        
        if (f <= 2)
            _x = x
            _y = y
        end
    next 
fend

fn TouchX()
    if _m = 0
        _x = _x / 3 #buffered ratio 3x
    end
    
    return GetW() - _x
fend

fn TouchY()
    if _m = 0
        _y = _y / 3 #buffered ratio 3x
    end
    
    return _y
fend

fn GetW() 
    if _m = 0
        if (_p = 1)
            return 80
        else 
            return 106
        end    
    else
        if (_p = 1)
            return 240
        else 
            return 320
        end
    end
fend

fn GetH()
    if _m = 0
        if (_p = 1)
            return 106
        else 
            return 80
        end    
    else
        if (_p = 1)
            return 320
        else 
            return 240
        end
    end
    
fend

fn SetDACtrl(s,r,c,b)
    v = 0
    
    if b = 1
        v = v | (1<<3)
    end
    
    if s = 1
        v = v | (1<<5)
    end
    
    if c = 1
        v = v | (1<<6)
    end
    
    if r = 1
        v = v | (1<<7)
    end
    
    LcdCmd(0x36, [v])
fend

fn SetWindow(x,y,w,h)
    e = x + w
    LcdCmd(0x2A, [x>> 8, x, e >> 8, e])
    
    e = y + h
    LcdCmd(0x2B, [y>> 8, y, e >> 8, e])
    
    LcdCmd(0x2c, [])
fend

Init(1, 0)
Show()

######################### 

```

</details>

[See full driver on GitHub](https://github.com/ghi-electronics/duelink-website/tree/main/static/code/drivers)

</TabItem>

<TabItem value="samples">

### Python

```python
from DUELink.DUELinkController import DUELinkController
import time
availablePort = DUELinkController.GetConnectionPort()
duelink = DUELinkController(availablePort)

x = 50
d = 10
width = int(float(duelink.Engine.Run("getw()")))
height = int(float(duelink.Engine.Run("geth()")))

while True:
    duelink.Graphics.Circle(1, x, height//2, 10)
    duelink.Graphics.Show()
    x = x + d
    if (x > width or x < 0):
        d = d * -1
        duelink.Graphics.Clear(0)
    time.sleep(0.1)
```

### JavaScript

```javascript
const { SerialUSB } = require("dlserialusb");
const { DUELinkController } = require("duelink");
```

### Script

```batch
Clear(0)
_t = 1 #_t = 0 test draw, _t =1 test touch

if _t = 0
    _i = 50
    _d = 10
    _w = GetW()

    while 1
        Circle(0xFF0000,_i,GetH()/2-40,10)
        Circle(0x00ff00,_i,GetH()/2-20,10)
        Circle(0x0000ff,_i,GetH()/2,10)
        Circle(0xFFffff,_i,GetH()/2+20,10)
        Show()
        _i=_i+_d
        if (_i > _w || _i <0)
            _d = _d * -1
            Clear(0)
        end
        Wait(10)
    wend
else
    while 1        
        Circle(0xFF0000,TouchX(),TouchY(),10)
        Show()       
        Wait(1)
    wend
end
```

[See full examples on GitHub](https://github.com/ghi-electronics/duelink-website/tree/main/static/code/samples)

</TabItem>

</Tabs>

---

<OrderSection
    product="DUELink TFT CP23"
    partnumber="GDL-DTFTCP23-B"
    price="$0.00"
/>
